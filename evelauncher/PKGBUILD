# Based on evelauncher
# Maintainer: Edmund Wu <fangkazuto@gmail.com>

pkgname=evelauncher
pkgver=1225328

pkgver () {
  printf "%s" $(curl -Ss http://binaries.eveonline.com/evelauncher_linux_release.json | grep -Po '[0-9]+')
}

pkgrel=1
pkgdesc='Unofficial Eve Online Launcher'
arch=('any')
url='https://forums.eveonline.com/t/eve-launcher-for-linux/7286'
license=('custom')
depends=('alsa-lib'
         'libpulse'
         'lib32-libldap'
         'lib32-openssl'
         'lib32-mpg123'
         'libwbclient'
         'openssl'
         'qt5-base'
         'qt5-translations'
         'qt5-webengine'
         'qt5-webkit'
         'qt5-webview'
         'qt5-websockets'
         'samba'
)
source=("https://binaries.eveonline.com/evelauncher-$(pkgver).tar.gz"
        'evelauncher.desktop'
        'EveOnline.png'
)
sha256sums=('SKIP'
            '35906fcc1041b3adff227d2f1fd8624599decc24012dbc8a9725a66ca2f3a767'
            'a87784e5b5f11d2cb5b881c31d4309165a96bbce7ec1a8ca088e07748203e259')

package () {
  # Install the main files.
  install -d "${pkgdir}/opt/${pkgname}"
  rm ${srcdir}/evelauncher/libQt5* ${srcdir}/evelauncher/QtWebEngineProcess
  rm -r "${srcdir}/evelauncher/plugins/"
  rm -r "${srcdir}/evelauncher/resources/"
  cp -a "${srcdir}/evelauncher/." "${pkgdir}/opt/${pkgname}"
  ln -s "/usr/lib/qt/libexec/QtWebEngineProcess" "${pkgdir}/opt/${pkgname}"
  ln -s "/usr/lib/qt/plugins/" "${pkgdir}/opt/${pkgname}"
  ln -s "/usr/share/qt/resources/" "${pkgdir}/opt/${pkgname}"
  ln -s "/usr/share/qt/translations/" "${pkgdir}/opt/${pkgname}"

  # Exec bit
  chmod 755 "${pkgdir}/opt/${pkgname}/evelauncher"

  # Desktop Entry
  install -d "${pkgdir}/usr/share/applications"
  install "${srcdir}/evelauncher.desktop" "${pkgdir}/usr/share/applications"

  install -d "${pkgdir}/usr/share/pixmaps"
  install "${srcdir}/EveOnline.png" "${pkgdir}/usr/share/pixmaps"

  # Main binary
 	install -d "${pkgdir}/usr/bin"
  ln -s "/opt/${pkgname}/evelauncher" "${pkgdir}/usr/bin/${pkgname}"
}
