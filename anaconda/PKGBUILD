# Based on anaconda
# Maintainer: Edmund Wu <fangkazuto@gmail.com>
pkgname=anaconda
pkgver=5.1.0

pkgver () {
  printf "%s" $(curl -Ss https://repo.continuum.io/archive/ | grep -Po "(?<=Anaconda3-)[0-9\.]+(?=-Linux-${CARCH})" | head -n 1)
}

pkgrel=1
pkgdesc='Completely free enterprise-ready Python distribution for large-scale data processing, predictive analytics, and scientific computing'
arch=('x86_64')
url='https://store.continuum.io/cshop/anaconda/'
license=('custom')
options=(!strip libtool staticlibs)
install="${pkgname}.install"
source=("http://repo.continuum.io/archive/Anaconda3-$(pkgver)-Linux-${CARCH}.sh"
        "${pkgname}.install")
sha512sums=('SKIP'
            '4959152c958a41543e52d71b2932f34b66c1b4fd6a6744124c63160b04feab03446ee98a824bfd17ffb4fc3cf967989fdfbf5410f7253c3382245bc47d82ff09')

prepare () {
  cd "${srcdir}"
  msg2 "Patching Anaconda3-$(pkgver)-Linux-${CARCH}.sh"
  sed \
    -e '/wc -c "\$THIS_PATH" | grep/s/!//' \
    -e "/export FORCE/s|$|;sed \"/^def update_prefix/a\\\    new_prefix='/opt/${pkgname}'\" -i pkgs/.install.py|" \
    -i "Anaconda3-$(pkgver)-Linux-${CARCH}.sh"
}

package () {
  prefix="${pkgdir}/opt/${pkgname}"
  LD_PRELOAD='/usr/lib/libfakeroot/libfakeroot.so'

  msg2 "Installing ${pkgname} to /opt/${pkgname}"
  bash "${srcdir}/Anaconda3-${pkgver}-Linux-${CARCH}.sh" -b -p "${prefix}" -f
  [ "$BREAK_EARLY" = 1 ] && exit 1
  cd "${prefix}"

  msg2 'Correcting permissions'
  chmod a+r -R pkgs

  msg2 'Stripping $pkgdir from default meta'
  find conda-meta -name '*.json' -exec sed -e "s/${pkgdir//\//\\\/}//g" -i {} \;

  msg2 'Installing license'
  install -D -m644 LICENSE.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
